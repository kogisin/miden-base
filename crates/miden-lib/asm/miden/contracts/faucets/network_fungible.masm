use.miden::active_account
use.miden::account_id
use.miden::active_note
use.miden::contracts::faucets
use.miden::contracts::faucets::basic_fungible

# CONSTANTS
# ================================================================================================

# The slot in this component's storage layout where the owner config is stored.
const.OWNER_CONFIG_SLOT=1

# ERRORS
const.ERR_ONLY_OWNER_CAN_MINT="note sender is not the owner of the faucet who can mint assets"

#! Checks if the note sender is the owner of this faucet.
#!
#! Inputs:  []
#! Outputs: [is_owner]
#!
#! Where:
#! - is_owner is 1 if the sender is the owner, 0 otherwise.
proc.is_owner
    push.OWNER_CONFIG_SLOT
    # => [owner_config_slot]

    exec.active_account::get_item
    # => [owner_prefix, owner_suffix, 0, 0]

    exec.active_note::get_sender
    # => [sender_prefix, sender_suffix, owner_prefix, owner_suffix, 0, 0]

    exec.account_id::is_equal
    # => [are_equal, 0, 0]

    movdn.2 drop drop
    # => [is_owner]
end

#! Distributes freshly minted fungible assets to the provided recipient.
#!
#! This procedure first checks if the note sender is the owner of the faucet, and then
#! mints the asset and creates an output note with that asset for the recipient.
#!
#! Inputs:  [amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(7)]
#! Outputs: [pad(16)]
#!
#! Where:
#! - amount is the amount to be minted and sent.
#! - tag is the tag to be included in the note.
#! - aux is the auxiliary data to be included in the note.
#! - note_type is the type of the note that holds the asset.
#! - execution_hint is the execution hint of the note that holds the asset.
#! - RECIPIENT is the recipient of the asset.
#!
#! Panics if:
#! - the note sender is not the owner of this faucet.
#! - any of the validations in faucets::distribute fail.
#!
#! Invocation: call
export.distribute
    exec.is_owner
    # => [is_owner, amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(7)]

    assert.err=ERR_ONLY_OWNER_CAN_MINT
    # => [amount, tag, aux, note_type, execution_hint, RECIPIENT, pad(7)]

    exec.faucets::distribute
    # => [pad(16)]
end

#! Burns the fungible asset from the active note.
#!
#! This procedure retrieves the asset from the active note and burns it. The note must contain
#! exactly one asset, which must be a fungible asset issued by this faucet.
#!
#! Inputs:  [pad(16)]
#! Outputs: [pad(16)]
#!
#! Panics if:
#! - the procedure is not called from a note context (active_note::get_assets will fail).
#! - the note does not contain exactly one asset.
#! - the transaction is executed against an account which is not a fungible asset faucet.
#! - the transaction is executed against a faucet which is not the origin of the specified asset.
#! - the amount about to be burned is greater than the outstanding supply of the asset.
#!
#! Invocation: call
export.faucets::burn
